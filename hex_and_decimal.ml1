MCSET S18=2
MCSKIP MT,<>
MCSKIP + WITHS NL
MCSKIP ; WITH ; NL
MCINS %.
MCINS U,$.
;;
;; reverse argument
MCDEF REV WITHS () AS <+
MCSET T1 = MCLENG(%A1.)
%L11.MCGO L10 IF T1 EN 0
MCSUB(%A1.,%T1.,%T1.)+
MCSET T1 = T1 - 1
MCGO L11
%L10.+
>
;;
;;
;; decimal -> hex
MCDEF HD0  WITH & AS 0
MCDEF HD1  WITH & AS 1
MCDEF HD2  WITH & AS 2
MCDEF HD3  WITH & AS 3
MCDEF HD4  WITH & AS 4
MCDEF HD5  WITH & AS 5
MCDEF HD6  WITH & AS 6
MCDEF HD7  WITH & AS 7
MCDEF HD8  WITH & AS 8
MCDEF HD9  WITH & AS 9
MCDEF HD10 WITH & AS A
MCDEF HD11 WITH & AS B
MCDEF HD12 WITH & AS C
MCDEF HD13 WITH & AS D
MCDEF HD14 WITH & AS E
MCDEF HD15 WITH & AS F
MCDEF D2Hr WITHS () AS <+
MCSET T1 = %A1.
%L1.+
MCGO L2 IF T1 GR 0
MCGO L0
%L2.+
MCSET T3 = T1 / 16
MCSET T2 = T1 - T3 * 16
MCSET T1 = T3
HD%T2.&+
MCGO L1
>
MCDEF D2H WITHS () AS <+
MCDEF <_> AS D2Hr(%A1.)
REV(_)>
;;
;; hex -> decimal
MCDEF HV WITH & WITH 0 AS 0
MCDEF HV WITH & WITH 1 AS 1
MCDEF HV WITH & WITH 2 AS 2
MCDEF HV WITH & WITH 3 AS 3
MCDEF HV WITH & WITH 4 AS 4
MCDEF HV WITH & WITH 5 AS 5
MCDEF HV WITH & WITH 6 AS 6
MCDEF HV WITH & WITH 7 AS 7
MCDEF HV WITH & WITH 8 AS 8
MCDEF HV WITH & WITH 9 AS 9
MCDEF HV WITH & WITH A AS 10
MCDEF HV WITH & WITH B AS 11
MCDEF HV WITH & WITH C AS 12
MCDEF HV WITH & WITH D AS 13
MCDEF HV WITH & WITH E AS 14
MCDEF HV WITH & WITH F AS 15
MCDEF H2D WITHS () AS <+
;; MCFOR T1 = 1 TO MCLENG(%A1.)
MCSET T1 = 1
MCSET T2 = MCLENG(%A1.)
MCSET T3 = 0
%L11.MCGO L10 IF T1 GR T2
MCDEF <_> AS HV&MCSUB(%A1.,%T1.,%T1.)
MCSET T3 = T3*16 + _
MCSET T1 = T1 + 1
MCGO L11
%L10.+
;; REPEAT+
%T3.>
---
H2D(AA)
D2H(170)
