MCSKIP MT,<>
MCSKIP + WITHS NL
MCSKIP NL WITH + NL N0
MCINS %.
+ remove NL
MCDEF RNL WITHS () AS <+
MCSET T2 = MCLENG(%A1.)
MCSET T1 = 1
%L2.+
MCGO L0 IF T1 GR T2
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <
>
MCSUB(%A1.,%T1.,%T1.)+
%L3.+
MCSET T1 = T1 + 1
MCGO L2
>
+ remove SPACE TAB and NL
MCDEF RSPACES WITHS ( ) AS <+
MCSET T2 = MCLENG(%A1.)
MCSET T1 = 1
%L2.+
MCGO L0 IF T1 GR T2
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = < >
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <	>
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <
>
MCSUB(%A1.,%T1.,%T1.)+
%L3.+
MCSET T1 = T1 + 1
MCGO L2
>
+ decimal -> hex
MCDEF HD0  AS 0
MCDEF HD1  AS 1
MCDEF HD2  AS 2
MCDEF HD3  AS 3
MCDEF HD4  AS 4
MCDEF HD5  AS 5
MCDEF HD6  AS 6
MCDEF HD7  AS 7
MCDEF HD8  AS 8
MCDEF HD9  AS 9
MCDEF HD10 AS A
MCDEF HD11 AS B
MCDEF HD12 AS C
MCDEF HD13 AS D
MCDEF HD14 AS E
MCDEF HD15 AS F
MCDEF HEXDG WITHS () AS <+
MCDEF <_> AS HD%A1.
_>
MCDEF DEC2HEX WITHS ( ) AS <+
MCSET T3 = %A1.
MCSET T1 = T3 / 16
MCSET T2 = T3 - T1 * 16
MCGO L1 IF %T1. EN 0
DEC2HEX(%T1.)%L1.HEXDG(%T2.)>
+ maximum
MCDEF MAX WITHS ( , ) AS <+
MCGO L1 IF %A2. GE %A1.
%A1.MCGO L0
%L1.+
%A2.>
+ field formatting
MCDEF FF WITHS ( , , ) AS <+
MCSET T2 = MCLENG(%A3.)
MCSET T3 = MAX(%A2.,%T2.)
MCSET T1 = T3 - T2
%L2.MCGO L1 UNLESS %T1. GE 1
%A1.+
MCSET T1 = T1 - 1
MCGO L2 
%L1.%A3.>
+ LV & TLV
MCDEF LV WITHS { } AS <+
MCDEF <_> AS %B1.
MCDEF <$2> AS RSPACES(_)
MCSET T2 = MCLENG($2) / 2
FF(0,2,DEC2HEX(%T2.))RNL(_)>
MCDEF TLV WITHS ( ) WITHS { } AS <%A1.LV{%B2.}>
------------------------
<
//Installing CAP file: *__miragt_app.cap 
//LV(Package AID) LV(Applet AID) LV(Applet AID) LV(?) LV(ext+param) LV(?)
//ext:   TLV(EA TLV(80 ?) TLV(81 LV(?) LV(?) LV(?) LV(USIM AID)))
//param: TLV(C9 LV(CONFIG_DATA) LV(SSM_AID) LV(2-init process) LV(USIM AID))
>
80E6 0C00 56 09FF434E525811010101 10FF434E525811010101000000FFFFFF00 10FF434E525811010101000000FFFFFF00 0100 26EA22 8008FF01200000000000 811600000010A0000000871002FF49FFFF89040B00FF0000 C900 00
80E6 0C00 LV{ LV{FF434E525811010101} LV{FF434E525811010101000000FFFFFF00} LV{FF434E525811010101000000FFFFFF00} LV{00} LV{TLV(EA){ TLV(80){FF01200000000000} TLV(81){00000010A0000000871002FF49FFFF89040B00FF0000}} TLV(C9){}} LV{}}
